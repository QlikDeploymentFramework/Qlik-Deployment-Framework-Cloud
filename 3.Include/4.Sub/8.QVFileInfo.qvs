// ___________________________________________________________________________
  //   Deployment Framework QVFileInfo sub function 8.QVFileInfo.qvs    
  //   
  //  This script will collect the tables and fields from the QVW and QVD files
  //  Fully Qualified file Name is the path and name of qvd or qvw file
  //  Table Name (Optional) is name of the table returning the result
  //  default table name is QVFileInfo linked with QVFileInfo_field (field details table)
  //  First include the QVFileInfo Function $(Include=$(vG.SubPath)\8.QVFileInfo.qvs);
  //  Usage QVFileInfo( 'Fully Qualified file Name',['Table Name'])
  //  Example: QVFileInfo('$(vG.QVDPath)\Customer.qvd')
  //  Example: QVFileInfo('$(vG.QVDPath)\Customer.qvd','QVFileTable')
  //  
  //  QVFileInfo works together with 5.DoDir.qvs 
  //  the field FullyQualifiedName will automatically link with DoDir result table
 //  ___________________________________________________________________________

SUB QVFileInfo(vL.QDF.QVFile,vL.QDF.QVTableName);

let vL.QDF.QVFile ='lib:/'&replace(mid(trim('$(vL.QDF.QVFile)'),6,len('$(vL.QDF.QVFile)')+6),'//','/'); //Remove any redundant slash in url

if '$(vL.QDF.QVTableName)'='' then // If No TableName Exists Use QVFileInfo
	SET vL.QDF.QVTableName = 'QVFileInfo';
endif;

Let vL.QvdNoOfFields=QvdNoOfFields('$(vL.QDF.QVFile)'); //count number of fields in table

if vL.QvdNoOfFields > 0 then // no point in loading if the file is empty or have faulty

unqualify QVType,QVFormat,QVTablesKey,FullyQualifiedName,QVTableName,QVFileTime,QVTableNbrRows,QVTableNbrFields,QVTableNbrKeyFields,QVTableComment,QVFileName,QVFieldName,QVTableCreator,QVComment,QVTableCreator;


[$(vL.QDF.QVTableName)]:
LOAD 
    upper('$(vL.QDF.QVFile)' & '_' & Date(QvdCreateTime('$(vL.QDF.QVFile)'),'YYYY-MM-DD') &'_' & QvdTableName('$(vL.QDF.QVFile)')) as QVTablesKey,
    '$(vL.QDF.QVFile)' as FullyQualifiedName,
    QvdTableName('$(vL.QDF.QVFile)')   as QVTableName,
    timestamp(QvdCreateTime('$(vL.QDF.QVFile)'),'$(TimestampFormat)') as QVFileTime,
      QvdNoOfRecords('$(vL.QDF.QVFile)')   as QVTableNbrRows,
      '$(vL.QvdNoOfFields)'  as QVTableNbrFields,
   // null()  as QVTableNbrKeyFields, // not used by QVD
    [Comment] as QVTableComment,
    subfield(replace('$(vL.QDF.QVFile)','/','\'),'\',-1) as QVFileName,
    [CreatorDoc] as QVTableCreator
    FROM [$(vL.QDF.QVFile)] (XmlSimple, Table is [QvdTableHeader]);	

[$(vL.QDF.QVTableName)_Fields]:
	LOAD [FieldName] as QVFieldName,
    	left([NumberFormat/Type],1) as QVType, // Keep only first letter as new QVD format only use first letter
    	[NumberFormat/Fmt] as QVFormat,
    	[Comment] as QVComment,
    		upper('$(vL.QDF.QVFile)' & '_' & Date(QvdCreateTime('$(vL.QDF.QVFile)'),'YYYY-MM-DD') &'_' & QvdTableName('$(vL.QDF.QVFile)')) as QVTablesKey
    		FROM [$(vL.QDF.QVFile)] (XmlSimple, Table is [QvdTableHeader/Fields/QvdFieldHeader]);

else
	trace '### DF Info 8.QVFileInfo.qvs Cant find any data in $(vL.QDF.QVFile)';
endif

// -----------------------------------------------
// Comment section for the table and its fields
// -----------------------------------------------
comment Field QVTablesKey with 'This field is a combination of the file name and table name of the QVW table.';
comment Field FullyQualifiedName with 'Fully Qualified Name of QlikView File.';
comment Field QVTableName with 'Table name of the table ';
comment Field QVTableNbrRows with 'Number of rows in the table.';
comment Field QVTableNbrFields with 'Number of fields in the table.';
comment Field QVTableComment with 'Table comment in the QVW table (loaded in the script).';


comment Field QVTablesKey with 'This field is a combination of the file name and table name of the QVW table.';
comment Field QVFieldName with 'QVW Field name in the QVW table that was scanned.';
comment Field QVWComment with 'Comment for the field (loaded in the script).';

// Clean up
SET vL.QDF.QVFile=;
SET vL.QDF.QVTableName=;
SET vL.QDF.QVTimeStamp=;
SET vL.QDF.FieldNo=;
set vL.QDF.IncompatibleQVD=;
set vL.QvdNoOfFields=;

END SUB

// *************************************
// Functions written by Magnus Berg Qlik
// *************************************